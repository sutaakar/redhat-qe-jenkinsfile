pipeline {
    agent {
        label "podman"
    }
    
    environment {
        WORKING_DIR = "kogito-cloud-operator"
        ENV_FILE = "${WORKSPACE}/.env"
    }
    
    stages {
        stage("Init"){
            steps{
                script {
                    sh """
                        podman login -u="${REGISTRY_USER}" -p="\${REGISTRY_PASSWORD}" ${IMAGE_REGISTRY}
                    """
                    
                    // Calculate build display name
                    setBuildDisplayName()
                    
                    // Temporary fix until java is set to version 11 by default
                    if (params.JAVA_HOME != "") {
                        env.JAVA_HOME = params.JAVA_HOME
                    }
                }
            }
        }
        stage("Checkout code"){
            steps {
                deleteDir()
                sh "rm -rf ${WORKING_DIR} && mkdir -p ${WORKING_DIR}"
                dir("${WORKING_DIR}") {
                    git url: "${REPOSITORY_URL}", branch: "${REPOSITORY_BRANCH}"
                }
            }
        }
        stage("Checkout pull request branch"){
            when {
                expression {
                    return env.PULL_REQUEST != '';
                }
            }
            steps {
                dir("${WORKING_DIR}") {
                    sh "git fetch origin pull/${PULL_REQUEST}/head:tempbranch"
                    sh "git checkout tempbranch"
                }
            }
        }
        stage("Setup node") {
            steps {
                script {
                    installGo()
                    installOperatorSDK()
                    installOpenshiftClient()
                    installMaven()
                    
                    sh """
                        . ${ENV_FILE}
                        
                        go version
                        hg version
                        python2 --version
                        operator-sdk version
                        oc version
                        mvn --version
                    """
                }
            }
        }
        
        stage("Build operator image"){
            steps {
                dir("${WORKING_DIR}") {
                    sh """
                        . ${ENV_FILE}
                        make image_builder=podman
                    """
                }
            }
        }
        
        stage("Build CLI"){
            steps {
                dir("${WORKING_DIR}") {
                    sh """
                        . ${ENV_FILE}
                        make build-cli
                    """
                }
            }
        }

        stage("Deploy operator image"){
            steps {
                dir("${WORKING_DIR}") {                    
                    sh """                        
                        podman login -u="${REGISTRY_USER}" -p="\${REGISTRY_PASSWORD}" ${IMAGE_REGISTRY}
                        podman tag ${getOperatorBuiltImageTaggedName()} ${getOperatorImageTaggedName()}
                        podman push ${getOperatorImageTaggedName()}
                    """
                }
            }
        }

        stage("Run smoke tests"){
            when {
                expression {
                    return !params.SKIP_SMOKE;
                }
            }
            steps {
                dir("${WORKING_DIR}") {
                    sh """
                        # Log into OpenShift so kubeconfig file is created
                        oc login ${OPENSHIFT_API_URL} -u ${OPENSHIFT_USER} -p \${OPENSHIFT_PASSWORD} --insecure-skip-tls-verify

                        . ${ENV_FILE}
                        
                        make run-smoke-tests load_default_config=true ci="${NAMESPACE_PREFIX}-smoke" concurrent=${CONCURRENT_TESTS} tags="${TEST_TAGS}" operator_image=${getOperatorImageName()} operator_tag=${getOperatorImageTag()} maven_mirror=${MAVEN_MIRROR_URL} build_image_version=${BUILD_IMAGE_VERSION} services_image_version=${SERVICES_IMAGE_VERSION} examples_uri=${EXAMPLES_URI} examples_ref=${EXAMPLES_REF} load_factor=${LOAD_FACTOR} keep_namespace=${KEEP_NAMESPACE}
                    """   
                }
            }
            post {
                always {
                    dir("${WORKING_DIR}") {
                        archiveArtifacts artifacts: 'test/logs/smoke/**/*.log', allowEmptyArchive: true
                        junit testResults: 'test/logs/smoke/junit.xml', allowEmptyResults: true
                    }
                }
            }
        }
        
        stage("Run Full tests"){
            when {
                expression {
                    return !params.SKIP_FULL;
                }
            }
            steps {
                dir("${WORKING_DIR}") {
                    sh """
                        # Log into OpenShift so kubeconfig file is created
                        oc login ${OPENSHIFT_API_URL} -u ${OPENSHIFT_USER} -p \${OPENSHIFT_PASSWORD} --insecure-skip-tls-verify
                        
                        . ${ENV_FILE}
                        
                        make run-tests load_default_config=true ci=${NAMESPACE_PREFIX} concurrent=${CONCURRENT_TESTS} tags="${TEST_TAGS}" operator_image=${getOperatorImageName()} operator_tag=${getOperatorImageTag()} maven_mirror=${MAVEN_MIRROR_URL} build_image_version=${BUILD_IMAGE_VERSION} services_image_version=${SERVICES_IMAGE_VERSION} examples_uri=${EXAMPLES_URI} examples_ref=${EXAMPLES_REF} load_factor=${LOAD_FACTOR} keep_namespace=${KEEP_NAMESPACE}
                    """   
                }
            }
            post {
                always {
                    dir("${WORKING_DIR}") {
                        archiveArtifacts artifacts: 'test/logs/full/**/*.log', allowEmptyArchive: true
                        junit testResults: 'test/logs/full/junit.xml', allowEmptyResults: true
                    }
                }
            }
        }
    }
     post {
        unstable {
            script {
                sendMail()
            }
        }
        failure {
            script {
                sendMail()
            }
        }
        changed {
            script {
                sendMail()
            }
        }
        always {
            dir("${WORKING_DIR}") {
                deleteDir()
            }
        }
    }
}

void setBuildDisplayName(){
    displayName = ""
    if(env.PULL_REQUEST != '') {
        displayName = "PR-${PULL_REQUEST}"
    } else {
        displayName = "${REPOSITORY_BRANCH}"
    }
    if(env.TEST_TAGS != '') {
        displayName += " (${TEST_TAGS})"
    }
    currentBuild.displayName = displayName
}

String getOperatorBuiltImageTaggedName() {
    return sh(script: "cat deploy/operator.yaml | grep image: | awk -F':' '{print \$2\":\"\$3}' | tr -d ' '", returnStdout: true).trim()
}

String getOperatorImageTaggedName() {
    return "${getOperatorImageName()}:${getOperatorImageTag()}"
}

String getOperatorImageName() {
    return "${IMAGE_REGISTRY}/${IMAGE_GROUP}/kogito-cloud-operator"
}

String getOperatorImageTag(){
    return env.PULL_REQUEST != '' ? "PR-${PULL_REQUEST}" : "${REPOSITORY_BRANCH}"
}

void sendMail(){
    mail to: "${EMAIL_REPORTING}", subject: "${currentBuild.currentResult} for job ${JOB_NAME}", body: "${BUILD_URL}"
}

void installGo(){
    // Download and install Go binary
    sh """
        rm -rf go/
        GO_VERSION=1.13.6
        GO_PACKAGE="go\${GO_VERSION}.linux-amd64.tar.gz"
        curl -LO https://dl.google.com/go/\${GO_PACKAGE} -o ${WORKSPACE}/\${GO_PACKAGE}
        tar xzf ${WORKSPACE}/\${GO_PACKAGE}
        chmod u+x ${WORKSPACE}/go/bin/*
    """

    // Set go env variables into env file
    sh '''
        rm -rf ${ENV_FILE}

        echo \"export GOROOT=${WORKSPACE}/go\" >> ${ENV_FILE}

        mkdir -p ${WORKSPACE}/go-${GO_VERSION}
        mkdir -p ${WORKSPACE}/go-${GO_VERSION}/.cache
        
        echo \"export GOCACHE=${WORKSPACE}/go-${GO_VERSION}/.cache\" >> ${ENV_FILE}
        echo \"export GOPATH=${WORKSPACE}/go-${GO_VERSION}\" >> ${ENV_FILE}
        
        . ${ENV_FILE}
        
        echo 'export PATH=\${PATH}:\${GOROOT}/bin' >> ${ENV_FILE}
        echo 'export PATH=\${PATH}:\${GOPATH}/bin' >> ${ENV_FILE}
        
        cat ${ENV_FILE}
    '''

    // Install Go dependencies
    sh """
        . ${ENV_FILE}
        export GO111MODULE=on
        go get github.com/gobuffalo/packr/v2/packr2
        go get -u golang.org/x/lint/golint
    """
}

void installOperatorSDK(){
    sh """
        # Install mercurial, needed by operator-sdk
        sudo yum install -y python2
        sudo yum install -y --skip-broken '*mercurial*'
        
        # Install operator-sdk 0.15.1
        rm -rf \$HOME/bin/operator-sdk
        RELEASE_VERSION=v0.15.1
        curl -LO https://github.com/operator-framework/operator-sdk/releases/download/\${RELEASE_VERSION}/operator-sdk-\${RELEASE_VERSION}-x86_64-linux-gnu
        #curl -LO https://github.com/operator-framework/operator-sdk/releases/download/\${RELEASE_VERSION}/operator-sdk-\${RELEASE_VERSION}-x86_64-linux-gnu.asc
        #gpg --verify operator-sdk-\${RELEASE_VERSION}-x86_64-linux-gnu.asc
        chmod +x operator-sdk-\${RELEASE_VERSION}-x86_64-linux-gnu && mkdir -p \$HOME/bin/ && cp operator-sdk-\${RELEASE_VERSION}-x86_64-linux-gnu \$HOME/bin/operator-sdk && rm operator-sdk-\${RELEASE_VERSION}-x86_64-linux-gnu
    """
}

void installOpenshiftClient(){
    sh """
        # Download and unzip latest OC client
        URL="https://mirror.openshift.com/pub/openshift-v4/clients/ocp/latest/"
        PACKAGE_URL=`curl \$URL --silent | grep "openshift-client-linux.tar.gz" | sed -n 's/.*href="\\([^"]*\\).*/\\1/p'`
        PACKAGE_URL="\${URL}\${PACKAGE_URL}"
        PACKAGE_NAME=`basename \${PACKAGE_URL}`
        wget \${PACKAGE_URL}
        tar xzf ./\${PACKAGE_NAME}
        mv oc \${OSE_CLIENT_DIR}
    """
}

void installMaven(){
    sh """       
        rm -rf \$HOME/bin/maven
        RELEASE_VERSION=3.6.3
        curl -LO https://mirrors.ocf.berkeley.edu/apache/maven/maven-3/\${RELEASE_VERSION}/binaries/apache-maven-\${RELEASE_VERSION}-bin.tar.gz
        tar xzf apache-maven-\${RELEASE_VERSION}-bin.tar.gz
        
        echo \"export MVN_HOME=${WORKSPACE}/apache-maven-\${RELEASE_VERSION}\" >> ${ENV_FILE}
        . ${ENV_FILE}
        echo 'export PATH=\${PATH}:\${MVN_HOME}/bin' >> ${ENV_FILE}
    """
}